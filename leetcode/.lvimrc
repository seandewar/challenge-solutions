if !g:localvimrc_sourced_once
    " Add comment markers and trims trailing whitespace for a list of lines
    function! s:CommentifyLines(lines) abort
        if !empty(&commentstring)
            call map(a:lines, {_, v -> printf(&commentstring, ' ' . v . ' ')})
        endif
        call map(a:lines, {_, v -> substitute(v, '^\(.\{-}\)\s*$', '\1', '')})
        return a:lines
    endfunction

    " Inserts main solution header at beginning of file
    function! s:AddLcMainHeader() abort
        let bname_no_ext = expand('%:t:r')
        let lines = ['https://leetcode.com/problems/'
                    \ . (empty(bname_no_ext) ? 'TODO' : bname_no_ext),
                    \ '',
                    \ 'Complexity: runtime O(TODO), space O(TODO).']
        let lines = s:CommentifyLines(lines) + ['']
        call append(0, lines)
    endfunction

    " Inserts alternative solution header above cursor line
    function! s:AddLcAltHeader() abort
        let lines = ['Alternative Solution: TODO.',
                    \ 'Complexity: runtime O(TODO), space O(TODO).']
        let lines = s:CommentifyLines(lines) + ['']
        call append(line('.') - 1, lines)
    endfunction

    function! s:SetupBuffer() abort
        if !empty(&buftype) || !&modifiable
            return
        endif

        if line('$') == 1 && empty(getline(1))
            LCMainHeader
        endif

        setlocal spell
        redraw " force a redraw now so the message has a chance to show
        echo "Use :LCAltHeader to add a header for an alternative solution."
    endfunction
endif

if g:localvimrc_sourced_once_for_file
    finish
endif

" make these commands available for all buffers just in case; this also makes
" this script fail in the sandbox when sourced, which will make localvimrc try
" to source this script outside of it, which is required for the timer callback
command! -bar -buffer LCMainHeader call s:AddLcMainHeader()
command! -bar -buffer LCAltHeader call s:AddLcAltHeader()

" some plugin-specific buffers don't set flags such as buftype until after the
" buffer is created, which can cause us to assume that they are file buffers;
" wait a bit for the plugin to set the buffer flags first
call timer_start(0, {-> s:SetupBuffer() })
